plugins {
    id 'java'
    id 'war'
}

group 'app.api'
version '1.0'

sourceCompatibility = 1.8

ext.docker = "${rootDir}/docker"

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'net.lingala.zip4j'            , name: 'zip4j'                , version: '1.3.2'
    compile group: 'com.google.inject'            , name: 'guice'                , version: '4.1.0'
    compile group: 'com.google.inject.extensions' , name: 'guice-assistedinject' , version: '4.1.0'
    compile group: 'com.sun.jersey'               , name: 'jersey-server'        , version: '1.19'
    compile group: 'com.sun.jersey'               , name: 'jersey-core'          , version: '1.19'
    compile group: 'com.sun.jersey'               , name: 'jersey-servlet'       , version: '1.19'

    testCompile group: 'junit'                    , name: 'junit'                , version: '4.12'
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
    }
    test {
        java {
            srcDirs = ['src/tests/java']
        }
    }
}

tasks.withType(JavaCompile) {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint'
    options.compilerArgs << '-deprecation'
}

clean {
    delete fileTree(dir: docker, include: "**/${project.name}*.war")
}

task copyWarToDocker(type: Copy) {
    def warCopySpec = copySpec {
        from war.archivePath
        rename { "${project.name}.war" }
    }

    dependsOn war
    into(docker)
    into('develop') { with warCopySpec }
}

war.finalizedBy(copyWarToDocker)

test {
    exclude '**/integration/**'

    reports {
        junitXml.enabled = true
        html.enabled = true
    }
}

jar {
    manifest {
        attributes(
                'Main-Class': 'App.App'
        )
    }
    from {
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

task integration(type: Test) {
    dependsOn test
    include '**/integration/**'
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.2.1'
}